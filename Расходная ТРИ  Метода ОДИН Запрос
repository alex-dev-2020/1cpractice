
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	МетодСписания = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(МоментВремени()).МетодСписания;
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	Движения.Продажи.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = "
	|Выбрать
	|	Т.Номенклатура
	|	,Т.Ссылка.Склад КАК Склад
	|	,Сумма(Т.Сумма) / Сумма(Т.Количество) КАК Цена
	|	,Сумма(Т.Количество) КАК Количество
	|	,Сумма(Т.Сумма) КАК Сумма
	|Поместить ВТ_Товары
	|	ИЗ Документ.РасходнаяНакладная.СписокНоменклатуры КАК Т
	|Где
	|	Т.Ссылка = &Ссылка
	|	//И Т.Номенклатура.ВидНоменклатуры = Значение(Перечисление.ВидыНоменклатуры.Товар)
	|Сгруппировать ПО 
	|	Т.Номенклатура
	|	,Т.Ссылка.Склад
	|Индексировать ПО
	|	Т.Номенклатура
	|	,Т.Ссылка.Склад
	|;
	|Выбрать
	|	Т.*
	|	,Т.Номенклатура.Представление КАК ПредставлениеНоменклатуры
	|	,Т.Склад.Представление КАК ПредставлениеСклада
	|	,ЕстьNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток
	|	,Остатки.СебестоимостьОстаток
	|"+?(МетодСписания = Перечисления.УчетнаяПолитика.ПоСредней, "", "	,Остатки.Партия") + "
	|ИЗ ВТ_Товары КАК Т
	|	Левое Соединение РегистрНакопления.ОстаткиНоменклатуры.Остатки(&МоментВремени
	|													,Номенклатура В (Выбрать Номенклатура ИЗ ВТ_Товары)
	|														И Склад В (Выбрать Склад ИЗ ВТ_Товары Сгруппировать ПО Склад)) КАК Остатки
	|		ПО Остатки.Номенклатура = Т.Номенклатура
	|		И Остатки.Склад = Т.Склад
	|"+?(МетодСписания = Перечисления.УчетнаяПолитика.ПоСредней, "", "
	|УПОРЯДОЧИТЬ ПО
	|	Остатки.Партия.МоментВремени "+?(МетодСписания = Перечисления.УчетнаяПолитика.ЛИФО, "УБЫВ", "")) + "
	|Итоги 
	|	Максимум(Т.Количество)
	|	,Максимум(Т.Сумма)
	|	,Максимум(Т.Цена)
	|	,Сумма(КоличествоОстаток)
	|	,Минимум(ПредставлениеНоменклатуры)
	|	,Минимум(ПредставлениеСклада)
	|ПО
	|	Т.Номенклатура
	|";
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар
			И ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "На складе """ + ВыборкаНоменклатура.ПредставлениеСклада + """ остатка товара """ + ВыборкаНоменклатура.ПредставлениеНоменклатуры + """
								|не хватает для отгрузки: в наличии - " + ВыборкаНоменклатура.КоличествоОстаток + ", нужно - " + ВыборкаНоменклатура.Количество;
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
		
		Если Не Отказ Тогда
			ВыборкаПартий = ВыборкаНоменклатура.Выбрать();
			ОсталосьСписать = ВыборкаНоменклатура.Количество;
			Себестоимость = 0;
			Если ВыборкаНоменклатура.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар Тогда
				Пока ВыборкаПартий.Следующий() Цикл
					Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
					Движение.Период = Дата;
					ЗаполнитьЗначенияСвойств(Движение, ВыборкаПартий);
					Движение.Количество = ?(ОсталосьСписать > ВыборкаПартий.КоличествоОстаток, ВыборкаПартий.КоличествоОстаток, ОсталосьСписать);
					Движение.Себестоимость = ?(ОсталосьСписать > ВыборкаПартий.КоличествоОстаток, ВыборкаПартий.СебестоимостьОстаток,
												Движение.Количество * (ВыборкаПартий.СебестоимостьОстаток / ВыборкаПартий.КоличествоОстаток));
					//Движение.Цена = Движение.Себестоимость / Движение.Количество;
					ОсталосьСписать = ОсталосьСписать - ВыборкаПартий.КоличествоОстаток;
					Себестоимость = Себестоимость + Движение.Себестоимость;
					Если ОсталосьСписать <= 0 Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Движение = Движения.Продажи.Добавить();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
			Движение.Количество = ВыборкаНоменклатура.Количество;
			Движение.Себестоимость = Себестоимость;
			Движение.СуммаПродажи = ВыборкаНоменклатура.Количество * ВыборкаНоменклатура.Цена;
			
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
